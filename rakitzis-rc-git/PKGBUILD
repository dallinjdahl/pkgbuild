pkgname=rakitzis-rc-git
pkgver=1.7.4.r117.g8ca9ab1
pkgrel=1
pkgdesc='Byron Rakitzis implementation of the rc shell'
arch=(i686 x86_64 armv7h aarch64)
license=(ZLIB)
depends=()
makedepends=(git)
provides=(rc)
source=(git+https://github.com/rakitzis/rc)
sha256sums=(SKIP)

pkgver() {
	cd "$srcdir/rc"
	git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/rc"
	autoreconf --install
	automake --add-missing
	./configure --with-edit=edit --prefix=/usr
}

build() {
	cd "$srcdir/rc"
	make
}

package() {
	cd "$srcdir/rc"
	make DESTDIR="$pkgdir" install
	install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
