pkgname=neatroff-latinmodern
pkgver=1
pkgrel=1
pkgdesc='Modernized troff clone: latinmodern fonts'
arch=(any)
license=(unknown)
makedepends=(git neatmkfn)
source=(http://mirrors.ctan.org/fonts/lm.zip)
sha256sums=(SKIP)

prepare() {
	mkdir "$srcdir/font"
	cd "$srcdir/font"
	for x in ../lm/fonts/afm/public/lm/*.afm
	do
		cp $x `basename $x .afm | tr a-z A-Z`.afm
	done
	for x in ../lm/fonts/type1/public/lm/*.pfb
	do
		mv $x `basename $x .pfb | tr a-z A-Z`.pfb
	done
}

build() {
	mkdir -p "$srcdir/devutf"

	find "$srcdir/font/" -name '*.afm' | while read FN
	do
		BFN=$(basename "$FN" .afm)
		DIR=$(dirname "$FN")
		echo $BFN
		T1=$DIR/$BFN.t1
		test -f "$T1" || T1=$DIR/$BFN.pfa
		test -f "$T1" || T1=$DIR/$BFN.pfb
		neatmkfn -a -b -r720 -t"$BFN" -f "$T1" <"$FN" | \
			sed "/^ligatures /s/ \(ct\|st\|sp\|Rp\)//g" >"$srcdir/devutf/$BFN"
	done

	for f in "$srcdir/devutf"/*
	do
		sed "/^fontpath /s=$srcdir/font=/usr/share/neatroff/font=" <$f >.fd.tmp
		mv .fd.tmp $f
	done
}

package() {
	cd "$srcdir/font"
	install -Dm644 -t "$pkgdir/usr/share/neatroff/font" *
	cd "$srcdir/devutf"
	install -Dm644 -t "$pkgdir/usr/share/neatroff/devutf" *
}

